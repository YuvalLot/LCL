<subs>

?nations >> [?n1, ?n2, ?n3, ?n4, ?n5]
?colors >> [?c1, ?c2, ?c3, ?c4, ?c5]
?beverages >> [?b1, ?b2, ?b3, ?b4, ?b5]
?smokes >> [?s1, ?s2, ?s3, ?s4, ?s5]
?pets >> [?p1, ?p2, ?p3, ?p4, ?p5]

</subs>

import List;
import @riddle_tools;

(*

Prompt:
A. In a street there are five houses, painted five different colors.
B. In each house lives a person of different nationality
C. These five homeowners each drink a different kind of beverage, smoke different brand of cigar and keep a different pet.

Hints:
1. The Brit lives in a red house.
2. The Swede keeps dogs as pets.
3. The Dane drinks tea.
4. The Green house is next to, and on the left of the White house.
5. The owner of the Green house drinks coffee.
6. The person who smokes Pall Mall rears birds.
7. The owner of the Yellow house smokes Dunhill.
8. The man living in the centre house drinks milk.
9. The Norwegian lives in the first house.
10. The man who smokes Blends lives next to the one who keeps cats.
11. The man who keeps horses lives next to the man who smokes Dunhill.
12. The man who smokes Blue Master drinks beer.
13. The German smokes Prince.
14. The Norwegian lives next to the blue house.
15. The man who smokes Blends has a neighbour who drinks water.

Question: Who owns the fish?

*)

set Color case red case green case yellow case blue case white;
set Nation case Brit case Swede case Dane case Norwegian case German;
set Beverage case milk case tea case coffee case beer case water;
set Smokes case pallmall case dunhill case blends case prince case bluemaster;
set Pet case dog case bird case fish case horse case cat;

domain EinsteinRiddle
   over ?c1, ?n1, ?b1, ?s1, ?p1,
        ?c2, ?n2, ?b2, ?s2, ?p2,
        ?c3, ?n3, ?b3, ?s3, ?p3,
        ?c4, ?n4, ?b4, ?s4, ?p4,
        ?c5, ?n5, ?b5, ?s5, ?p5

   of ?c1 : Color(?c1)
   of ?n1 : Nation(?n1)
   of ?b1 : Beverage(?b1)
   of ?s1 : Smokes(?s1)
   of ?p1 : Pet(?p1)

   of ?c2 : Color(?c2)
   of ?n2 : Nation(?n2)
   of ?b2 : Beverage(?b2)
   of ?s2 : Smokes(?s2)
   of ?p2 : Pet(?p2)

   of ?c3 : Color(?c3)
   of ?n3 : Nation(?n3)
   of ?b3 : Beverage(?b3)
   of ?s3 : Smokes(?s3)
   of ?p3 : Pet(?p3)

   of ?c4 : Color(?c4)
   of ?n4 : Nation(?n4)
   of ?b4 : Beverage(?b4)
   of ?s4 : Smokes(?s4)
   of ?p4 : Pet(?p4)

   of ?c5 : Color(?c5)
   of ?n5 : Nation(?n5)
   of ?b5 : Beverage(?b5)
   of ?s5 : Smokes(?s5)
   of ?p5 : Pet(?p5)

   elim repeat(?nations)
   elim repeat(?colors)
   elim repeat(?pets)
   elim repeat(?beverages)
   elim repeat(?smokes)

   # The Brit lives in a red house.
   const OneOf(Brit, red, ?nations, ?colors)
   # The Swede keeps dogs as pets.
   const OneOf(Swede, dog, ?nations, ?pets)
   # The Dane drinks tea.
   const OneOf(Dane, tea, ?nations, ?beverages)
   # The Green house is next to, and on the left of the White house.
   const ExactlyLeft(green, white, ?colors, ?colors)
   # The owner of the Green house drinks coffee.
   const OneOf(green, coffee, ?colors, ?beverages)
   # The person who smokes Pall Mall rears birds.
   const OneOf(pallmall, bird, ?smokes, ?pets)
   # The owner of the Yellow house smokes Dunhill
   const OneOf(yellow, dunhill, ?colors, ?smokes)
   # The man living in the centre house drinks milk
   const E(?b3, milk)
   # The Norwegian lives in the first house.
   const E(?n1, Norwegian)
   # The man who smokes Blends lives next to the one who keeps cats.
   const NextTo(blends, cat, ?smokes, ?pets)
   # The man who keeps horses lives next to the man who smokes Dunhill.
   const NextTo(horse, dunhill, ?pets, ?smokes)
   # The man who smokes Blue Master drinks beer.
   const OneOf(bluemaster, beer, ?smokes, ?beverages)
   # The German smokes Prince.
   const OneOf(German, prince, ?nations, ?smokes)
   # The Norwegian lives next to the blue house.
   const NextTo(Norwegian, blue, ?nations, ?colors)
   # The man who smokes Blends has a neighbour who drinks water.
   const NextTo(blends, water, ?smokes, ?beverages);

set Nice
   case then
      EinsteinRiddle(?c1, ?n1, ?b1, ?s1, ?p1,
           ?c2, ?n2, ?b2, ?s2, ?p2,
           ?c3, ?n3, ?b3, ?s3, ?p3,
           ?c4, ?n4, ?b4, ?s4, ?p4,
           ?c5, ?n5, ?b5, ?s5, ?p5) &
      Print("The ?n1 lives in the ?c1 house, drinks ?b1, smokes ?s1 and has ?p1") &
      Print("The ?n2 lives in the ?c2 house, drinks ?b2, smokes ?s2 and has ?p2") &
      Print("The ?n3 lives in the ?c3 house, drinks ?b3, smokes ?s3 and has ?p3") &
      Print("The ?n4 lives in the ?c4 house, drinks ?b4, smokes ?s4 and has ?p4") &
      Print("The ?n5 lives in the ?c5 house, drinks ?b5, smokes ?s5 and has ?p5");
